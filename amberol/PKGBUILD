# Maintainer: Kevin Gavrois <kevin@gavrois.fr>

_pkgname=amberol
pkgname=${_pkgname}-cactus
pkgver=0.8.1.r14.g79f63eb
pkgrel=1
pkgdesc="Plays music, and nothing else"
arch=('x86_64')
url="https://gitlab.gnome.org/ebassi/amberol"
license=('GPL3')
depends=( 'gtk4' 'libadwaita' 'gstreamer' 'gst-plugins-base' 'gst-plugins-bad' 'gst-plugins-good')
makedepends=('rust' 'meson' 'm4' 'appstream-glib' 'reuse' 'patch')
#checkdepends=('appstream-glib' 'reuse')
optdepends=()
provides=()
conflicts=()
replaces=()
backup=()
options=()
install=
changelog=
source=(
	"$_pkgname::git+https://gitlab.gnome.org/ebassi/amberol.git"
	'ashpd.patch'
	'logger.patch')
sha512sums=(
	'SKIP'
	'a76b68f437d77c9f83c992744988beeac55d65f29f33c1acf03d0feb8cd55ca4970c55ef5b555c87443deb7a5bb77e12e7a292d4e4be4dbb19c4f8c5582a236a'
	'bbb825e5a27863bafc15057e87846b3de5a4d5c5966dc769af5b99827b02d3bc83678fa23da067ca4e59a1eb78ae3a419688d734db4f776b063b5cf3e81e3d9b')

pkgver() {
	cd $_pkgname
	git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd $_pkgname
	patch -p1 < ../ashpd.patch
	patch -p1 < ../logger.patch
}

build() {
	arch-meson $_pkgname build
	meson compile -C build
}

check() {
	meson test -C build --print-errorlogs
}

package() {
	DESTDIR="$pkgdir" meson install -C build
}
