_pkgname=celluloid
pkgname=${_pkgname}-cactus
pkgver=0.23.r24.gc59f739
pkgrel=1
pkgdesc='Simple GTK+ frontend for mpv'
arch=('x86_64')
url='https://celluloid-player.github.io/'
license=('GPL3')
depends=('gtk4' 'libadwaita' 'mpv' 'libmpv.so')
makedepends=('appstream-glib' 'meson')
conflicts=('gnome-mpv' 'celluloid')
provides=('gnome-mpv' 'celluloid')
replaces=('gnome-mpv-git')
source=(
	"$_pkgname::git+https://github.com/celluloid-player/celluloid.git")
sha256sums=(
	'SKIP')

pkgver() {
	cd $_pkgname
	git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	arch-meson $_pkgname build
	meson compile -C build
}

check() {
	meson test -C build --print-errorlogs
}

package() {
	DESTDIR="$pkgdir" meson install -C build
}

