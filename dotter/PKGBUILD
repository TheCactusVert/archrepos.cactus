# Maintainer: Kevin Gavrois <kevin@gavrois.fr>

pkgname=dotter-rs
_pkgname=dotter
pkgver=0.12.6
pkgrel=1
pkgdesc='A dotfile manager and templater written in Rust'
arch=('x86_64' 'armv7h')
url='https://github.com/SuperCuber/dotter'
license=('Unlicense')
conflicts=($_pkgname)
depends=('gcc-libs')
makedepends=('rust')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('SKIP')

build() {
	cd $_pkgname-$pkgver
	cargo build --release --locked
}

check() {
	cd $_pkgname-$pkgver
	cargo test --release --locked
}

package() {
	cd $_pkgname-$pkgver
	install -Dm755 target/release/$_pkgname -t $pkgdir/usr/bin
	install -Dm644 README.md -t $pkgdir/usr/share/doc/$pkgname
}
