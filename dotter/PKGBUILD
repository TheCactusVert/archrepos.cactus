# Maintainer: Kevin Gavrois <kevin@gavrois.fr>

_pkgname=dotter
pkgname=dotter-rs
pkgver=0.12.7.r3.ga2955fe
pkgrel=1
pkgdesc='A dotfile manager and templater written in Rust'
arch=('x86_64' 'aarch64')
url='https://github.com/SuperCuber/dotter'
license=('Unlicense')
conflicts=($_pkgname)
depends=('gcc-libs')
makedepends=('rust')
source=("git+https://github.com/SuperCuber/$_pkgname.git")
sha512sums=('SKIP')

pkgver() {
       cd $_pkgname
       git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd $_pkgname
	cargo build --release --locked
}

check() {
	cd $_pkgname
	cargo test --release --locked
}

package() {
	cd $_pkgname
	install -Dm755 target/release/$_pkgname -t $pkgdir/usr/bin
	install -Dm644 README.md -t $pkgdir/usr/share/doc/$pkgname
}
