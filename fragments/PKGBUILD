# Maintainer: Kevin Gavrois <kevin@gavrois.fr>

pkgname=fragments-cactus
pkgver=1.5
pkgrel=1
pkgdesc="BitTorrent client for GNOME"
arch=(i686 x86_64 armv6h armv7h)
url="https://gitlab.gnome.org/World/Fragments"
license=(GPL3)
depends=(curl libb64 libevent libhandy libnatpmp miniupnpc openssl)
makedepends=(git gobject-introspection libdazzle libhandy meson vala dht libutp libtransmission)
provides=(fragments)
conflicts=(fragments)
_commit=4ad9b0d68e4a2f4fb03ebb55ca4adc5ddb7058ce
source=("git+https://gitlab.gnome.org/World/Fragments.git#commit=$_commit")
sha256sums=('SKIP')

prepare() {
	sed -i "s/meson.get_compiler('c').find_library('libcrypto'),/meson.get_compiler('c').find_library('libcrypto'), meson.get_compiler('c').find_library('libssl'),/g" Fragments/meson.build
}

build() {
	arch-meson Fragments build
	meson compile -C build
}

check() {
	meson test -C build --print-errorlogs
}

package() {
	DESTDIR="$pkgdir" meson install -C build
}
