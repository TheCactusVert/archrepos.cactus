# Maintainer: Kevin Gavrois <kevin@gavrois.fr>

_pkgname=git-delta
pkgname=$_pkgname-cactus
pkgver=0.13.0.r11.g5f8ac5f
pkgrel=7
pkgdesc='Syntax-highlighting pager for git and diff output'
arch=('x86_64' 'aarch64')
url='https://github.com/dandavison/delta'
license=('MIT')
depends=('libgit2' 'libgit2.so')
makedepends=('rust')
optdepends=()
provides=('git-delta')
conflicts=('git-delta' 'git-delta-git')
replaces=()
backup=()
options=()
install=
changelog=
source=(
	"$_pkgname::git+https://github.com/dandavison/delta.git"
	"zsh-completions")
sha512sums=(
	'SKIP'
	'011ba03c9e06ba6bcdeec9bf30faabbb07fbae0b846f772b8865d412ea94263089febdaf022fc1a4bda7ffa32ba0357edc2d557db4f2aaebfbdce884b3e7f4dc')
	
pkgver() {
       cd $_pkgname
       git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd $_pkgname

	# download dependencies
	cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
	cd $_pkgname
	cargo build --release --frozen --all-features
}

check() {
	cd $_pkgname
	cargo test --release --frozen --all-features
}

package() {
	cd $_pkgname
	install -Dm755 target/release/delta $pkgdir/usr/bin/$_pkgname
	install -Dm644 etc/completion/completion.bash $pkgdir/usr/share/bash-completion/completions/$_pkgname
	install -Dm644 $srcdir/zsh-completions $pkgdir/usr/share/zsh/site-functions/$_pkgname
}
