# Maintainer: Kevin Gavrois <kevin@gavrois.fr>

_pkgname=gnome-control-center
pkgname=$_pkgname-cactus
pkgver=41.0.r52.g77da0efd0
pkgrel=1
pkgdesc="GNOME's main interface to configure various aspects of the desktop - without Cheese dependency"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://gitlab.gnome.org/GNOME/$_pkgname"
license=('GPL2')
groups=('gnome')
depends=(
	'accountsservice' 'bolt' 'colord-gtk' 'cups-pk-helper' 'gnome-bluetooth'
	'gnome-color-manager' 'gnome-desktop' 'gnome-online-accounts'
	'gnome-settings-daemon' 'gsettings-desktop-schemas' 'gsound' 'gtk3'
	'libgnomekbd' 'libgtop' 'libgudev' 'libhandy' 'libibus' 'libmm-glib'
	'libpwquality' 'nm-connection-editor' 'power-profiles-daemon' 'smbclient'
	'sound-theme-freedesktop' 'udisks2' 'upower'
)
optdepends=(
	'gnome-user-share: WebDAV file sharing'
	'gnome-remote-desktop: screen sharing'
	'openssh: remote login'
	'rygel: media sharing'
	'system-config-printer: Printer settings'
)
makedepends=('docbook-xsl' 'meson' 'modemmanager' 'python')
checkdepends=('python-dbusmock' 'python-gobject' 'xorg-server-xvfb')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("git+https://gitlab.gnome.org/GNOME/$_pkgname.git")
sha256sums=('SKIP')

pkgver() {
       cd $_pkgname
       git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd $_pkgname
	git submodule update --init
}

build() {
	arch-meson $_pkgname build \
		-D documentation=true \
		-D cheese=false \
		-D snap=false
	meson compile -C build
}

check() {
	meson test -C build --print-errorlogs
}

package() {
	meson install -C build --destdir $pkgdir
	install -d -o root -g 102 -m 750 $pkgdir/usr/share/polkit-1/rules.d
}
