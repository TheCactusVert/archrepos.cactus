# Maintainer: Kevin Gavrois <kevin@gavrois.fr>

_pkgname=gnome-online-accounts
pkgname=$_pkgname-cactus
pkgver=3.44.0.r1.ge88296c4
pkgrel=1
pkgdesc="Single sign-on framework for GNOME"
arch=('x86_64' 'aarch64')
url="https://gitlab.gnome.org/GNOME/gnome-online-accounts"
license=(LGPL)
depends=(webkit2gtk json-glib libnotify rest libsecret)
makedepends=(gobject-introspection gtk-doc vala git meson)
optdepends=()
provides=($_pkgname libgoa-1.0.so libgoa-backend-1.0.so)
conflicts=($_pkgname)
replaces=()
backup=()
options=()
install=
changelog=
source=(
	"$_pkgname::git+https://gitlab.gnome.org/GNOME/gnome-online-accounts.git")
sha256sums=(
	'SKIP')

pkgver() {
       cd $_pkgname
       git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	arch-meson $_pkgname build \
		-D exchange=false \
		-D facebook=false \
		-D fedora=false \
		-D flickr=false \
		-D foursquare=false \
		-D google=false \
		-D kerberos=false \
		-D lastfm=false \
		-D owncloud=false \
		-D windows_live=false \
		-D media_server=false \
		-D gtk_doc=true \
		-D man=true
	meson compile -C build
}

check() {
	meson test -C build --print-errorlogs
}

package() {
	meson install -C build --destdir $pkgdir
}

