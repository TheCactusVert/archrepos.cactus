# Maintainer: Kevin Gavrois <kevin@gavrois.fr>

_pkgname=mpv
pkgname=lib${_pkgname}-cactus
pkgver=0.34.0.r342.gc961f4d0db
pkgrel=1
pkgdesc='A free, open source, and cross-platform media player'
arch=('x86_64' 'aarch64')
url='https://mpv.io/'
license=('GPL3')
depends=('alsa-lib' 'libasound.so' 'ffmpeg' 'libavcodec.so' 'libavdevice.so'
         'libavfilter.so' 'libavformat.so' 'libavutil.so' 'libswresample.so' 'libswscale.so'
         'glibc' 'jack' 'libjack.so' 'lcms2' 'liblcms2.so' 'libarchive'
         'libarchive.so' 'libass' 'libass.so' 'libbluray' 'libbluray.so' 'libcdio'
         'libcdio-paranoia' 'libdrm' 'libdvdnav' 'libdvdread' 'libegl' 'libgl' 'libglvnd'
         'libjpeg' 'libjpeg.so' 'libplacebo' 'libplacebo.so' 'libpulse' 'libpulse.so'
         'libva' 'libva.so' 'libva-drm.so' 'libva-wayland.so' 'libva-x11.so' 'libvdpau' 'libx11'
         'libxext' 'libxinerama' 'libxkbcommon' 'libxkbcommon.so' 'libxrandr' 'libxss'
         'libxv' 'lua52' 'mesa' 'shaderc' 'libshaderc_shared.so'
         'uchardet' 'vulkan-icd-loader' 'wayland' 'xdg-utils' 'zlib')
makedepends=('git' 'python-docutils' 'ladspa' 'wayland-protocols'
             'ffnvcodec-headers' 'vulkan-headers')
optdepends=('yt-dlp: for video-sharing websites playback'
            'youtube-dl: for video-sharing websites playback')
provides=($_pkgname 'libmpv.so')
conflicts=($_pkgname)
replaces=()
backup=()
options=('!emptydirs')
install=
changelog=
source=(
	"$_pkgname::git+https://github.com/mpv-player/mpv.git")
sha256sums=(
	'SKIP')

pkgver() {
       cd $_pkgname
       git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd $_pkgname
	./bootstrap.py
	./waf configure \
		--prefix=/usr \
		--confdir=/etc/mpv \
		--disable-cplayer \
		--disable-rubberband \
		--enable-cdda \
		--enable-dvb \
		--enable-dvdnav \
		--enable-libarchive \
		--enable-libmpv-shared \
		--disable-build-date \
		--disable-caca \
		--disable-javascript \
		--disable-cplugins
	./waf build
}

package() {
	cd $_pkgname
	./waf install --destdir=$pkgdir
}

