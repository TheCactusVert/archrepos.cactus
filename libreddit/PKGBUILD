pkgname='libreddit'
pkgver='0.14.14'
pkgrel='1'
pkgdesc='Alternative private front-end to Reddit'
arch=('x86_64' 'armv7h')
url='https://github.com/spikecodes/libreddit'
license=('AGPL-3.0')
makedepends=('git' 'cargo')
provides=($pkgname)
conflicts=($pkgname)
source=("https://github.com/spikecodes/${pkgname}/archive/refs/tags/v${pkgver}.tar.gz"
	"$pkgname.service"
	"$pkgname.sysusers.conf")
sha512sums=(
	'SKIP'
	'1515eeb892767e28d0d9bbf6476ad04e7df3769a5d5b322f0f36ab663deb76b92382d09b01c543cc4d8fad55fccdcd5dc98cf27c27f6c4cf54e86da7788701ae'
	'f70135070c1385cd1a2f16dbe65ac8c6bb44bcc66c02c3e0eefb3d75ebeab98aae26577974bfbf7bfc7176878a1d6c6f59213344b369b4ed9f4fea5f501dc569')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	cargo build --release --all-features --target-dir=target
}

package() {
	install -Dm644 $srcdir/$pkgname.service -t $pkgdir/usr/lib/systemd/system/
	install -Dm644 $srcdir/$pkgname.sysusers.conf $pkgdir/usr/lib/sysusers.d/$pkgname.conf
	install -Dm644 $srcdir/$pkgname-$pkgver/LICENSE -t $pkgdir/usr/share/licenses/$pkgname/
	install -Dm755 $srcdir/$pkgname-$pkgver/target/release/$pkgname -t $pkgdir/usr/bin/
}
