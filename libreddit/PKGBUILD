# Maintainer: Kevin Gavrois <kevin@gavrois.fr>

pkgname=libreddit
pkgver=0.15.2
pkgrel=1
pkgdesc='Alternative private front-end to Reddit'
arch=('x86_64' 'armv7h')
url='https://github.com/spikecodes/libreddit'
license=('AGPL-3.0')
makedepends=('git' 'cargo')
provides=($pkgname)
conflicts=($pkgname)
source=("https://github.com/spikecodes/${pkgname}/archive/refs/tags/v${pkgver}.tar.gz"
	"$pkgname.env"
	"$pkgname.service"
	"$pkgname.sysusers.conf")
sha512sums=(
	'SKIP'
	'59625b3846c40e6b5c2b866c2e6aeaece0052f051900c3feb0d42dc16bce0c9c9b6aa6b86fed9ac2634ed3b95de21cd5c0d2850a049e8849a94e7674ada14988'
	'18603beacc283f88d39e6509096f313bc58e1a3583675a57f215330d2b43b8a4a56cb32bb223eded3c2fb8c54e67d990ab62ab65e536d596e2ffde6e1d5d233c'
	'f70135070c1385cd1a2f16dbe65ac8c6bb44bcc66c02c3e0eefb3d75ebeab98aae26577974bfbf7bfc7176878a1d6c6f59213344b369b4ed9f4fea5f501dc569')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	cargo build --release --all-features --target-dir=target
}

package() {
	install -Dm644 $srcdir/$pkgname.env -t $pkgdir/etc/
	install -Dm644 $srcdir/$pkgname.service -t $pkgdir/usr/lib/systemd/system/
	install -Dm644 $srcdir/$pkgname.sysusers.conf $pkgdir/usr/lib/sysusers.d/$pkgname.conf
	install -Dm644 $srcdir/$pkgname-$pkgver/LICENSE -t $pkgdir/usr/share/licenses/$pkgname/
	install -Dm755 $srcdir/$pkgname-$pkgver/target/release/$pkgname -t $pkgdir/usr/bin/
}
