pkgname=libtransmission
pkgver=3.00
pkgrel=1
pkgdesc="Fast, easy, and free BitTorrent client (library)"
arch=(i686 x86_64 arm armv6h armv7h aarch64)
url="http://www.transmissionbt.com/"
license=(MIT)
depends=(openssl)
makedepends=(cmake libutp)
source=("git+https://github.com/transmission/transmission#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
	cd transmission
	git submodule update --init --recursive
	cd ..
	if [[ -d build ]]; then
		rm -rf build
	fi
	mkdir build
}

build() {
	cd build
  
	cmake ../transmission -DCMAKE_INSTALL_PREFIX="/usr" -DLIB_INSTALL_DIR='/usr/lib' -DCMAKE_INSTAL_LIBDIR="/usr/lib" -DINSTALL_LIB=ON -DENABLE_UTP=ON -DENABLE_GTK=OFF -DENABLE_QT=OFF -DENABLE_CLI=OFF -DENABLE_DAEMON=OFF  -DCMAKE_BUILD_TYPE=Release -DUSE_SYSTEM_UTP=ON -DWITH_CRYPTO=openssl
	make
}

package(){
	cd build
	make -C libtransmission DESTDIR="$pkgdir" install
	install -Dm644 $srcdir/transmission/COPYING "$pkgdir/usr/share/licenses/libtransmission/COPYING"
}
