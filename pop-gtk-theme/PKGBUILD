pkgname=pop-gtk-theme
pkgver="5.3.3"
pkgrel=1
pkgdesc="System76 Pop GTK+ Theme"
pkgcommit=c5f700d
arch=('any')
url='https://github.com/pop-os/gtk-theme'
license=('LGPL2.1' 'CCPL' 'GPL3')
makedepends=('sassc' 'meson')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/pop-os/gtk-theme/archive/${pkgcommit}.tar.gz")
sha512sums=('35fade9cd6c6cc6555dd7d9080482a8cf72603fd6a20fa632620f36adda4a14c0fd5a91646bf226406abdc9284ffd70a682498768ce437638d5ec0cf42fd93ee')

build() {
	cd "gtk-theme-${pkgcommit}"*
	# Removing GTK 2.0 because I don't need it.
	sed -i "/subdir('gtk-2.0')/d" gtk/src/*/meson.build
	meson --prefix='/usr' build
	ninja -C build
}

package() {
	cd "gtk-theme-${pkgcommit}"*
	DESTDIR="${pkgdir}" ninja -C build install

	# https://github.com/pop-os/gtk-theme/issues/436
	ln -s /usr/share/gnome-shell/theme/Pop-dark "${pkgdir}/usr/share/themes/Pop-dark/gnome-shell"
}

