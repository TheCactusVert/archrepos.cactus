# Maintainer: Kevin Gavrois <kevin@gavrois.fr>

_pkgname=gtk-theme
pkgname=pop-$_pkgname-cactus
pkgver=5.4.4.r13.g23d7ac83
pkgrel=1
pkgdesc='System76 Pop GTK+ Theme'
arch=('any')
url='https://github.com/pop-os/gtk-theme'
license=('LGPL2.1' 'CCPL' 'GPL3')
makedepends=('sassc' 'meson')
source=("git+https://github.com/pop-os/$_pkgname.git")
sha512sums=('SKIP')

pkgver() {
       cd $_pkgname
       git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd $_pkgname
	# Removing GTK 2.0 because I don't need it.
	sed -i "/subdir('gtk-2.0')/d" gtk/src/*/meson.build
}

build() {
	cd $_pkgname
	meson --prefix='/usr' build
	ninja -C build
}

package() {
	cd $_pkgname
	DESTDIR="${pkgdir}" ninja -C build install

	# https://github.com/pop-os/gtk-theme/issues/436
	ln -s /usr/share/gnome-shell/theme/Pop-dark $pkgdir/usr/share/themes/Pop-dark/gnome-shell
}

