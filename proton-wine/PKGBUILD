# Maintiner: anon at sansorgan.es
# Contributor: Sean Anderson <seanga2@gmail.com>
# Contributor: Daniel Bermond <danielbermond@yahoo.com>
# Contributor: Sidney Crestani <sidneycrestani@archlinux.net>
# Contributor: sxe <sxxe@gmx.de>
# Conttributor: xiretza <xiretza+aur@gmail.com>
# Contributor: heavysink <winstonwu91 at gmail>
pkgname=proton-wine
pkgver=6.3
pkgrel=4
pkgdesc='A compatibility layer for running Windows programs (Valve version)'
arch=('i686' 'x86_64')
url='https://github.com/ValveSoftware/wine.git'
license=('LGPL')
depends=(
    'sdl2'                  'lib32-sdl2' #OK
    'fontconfig'            'lib32-fontconfig' #OK
    'lcms2'                 'lib32-lcms2' #OK
    'libxml2'               'lib32-libxml2' #OK
    'libxslt'               'lib32-libxslt' #OK
    'libxcursor'            'lib32-libxcursor'
    'libxrandr'             'lib32-libxrandr'
    'libxdamage'            'lib32-libxdamage'
    'libxi'                 'lib32-libxi'
#    'gettext'               'lib32-gettext'
    'freetype2'             'lib32-freetype2' #OK
#    'glu'                   'lib32-glu'
    'libsm'                 'lib32-libsm'
    'gcc-libs'              'lib32-gcc-libs'
    'libpcap'               'lib32-libpcap' #OK
    'libgphoto2' #OK
    'faudio'                'lib32-faudio' #OK
    'vkd3d' #OK
)
makedepends=(
#    'autoconf'
#    'ncurses'
    'bison' #OK
#    'perl'
    'flex' #OK
    'gcc>=4.5.0-2' #OK
#    'giflib'                'lib32-giflib'
    'libpng'                'lib32-libpng' #OK
    'libjpeg-turbo'         'lib32-libjpeg-turbo' #OK
    'libtiff'               'lib32-libtiff' #OK
    'gnutls'                'lib32-gnutls' #OK
    'libxinerama'           'lib32-libxinerama'
    'libxcomposite'         'lib32-libxcomposite'
    'libxmu'                'lib32-libxmu'
    'libxxf86vm'            'lib32-libxxf86vm'
    'libldap'               'lib32-libldap' #OK
    'libcups'               'lib32-libcups' #OK
    'libusb'                'lib32-libusb' #OK
    'krb5'                  'lib32-krb5' #OK
    'mpg123'                'lib32-mpg123' #OK
    'openal'                'lib32-openal' #OK
    'v4l-utils'             'lib32-v4l-utils' #OK
    'libpulse'              'lib32-libpulse' #OK
    'alsa-lib'              'lib32-alsa-lib' #OK
    'libxcomposite'         'lib32-libxcomposite'
    'libunwind'             'lib32-libunwind' #OK
    'mesa'                  'lib32-mesa' #OK
#    'libgl'                 'lib32-libgl'
    'opencl-headers' #OK
    'ocl-icd'               'lib32-ocl-icd' #OK
    'libxslt'               'lib32-libxslt' #OK
    'gstreamer'             'lib32-gstreamer' #OK
    'gst-plugins-base-libs' 'lib32-gst-plugins-base-libs' #OK
    'vulkan-headers' #OK
    'vulkan-icd-loader'     'lib32-vulkan-icd-loader' #OK
    'smbclient' #OK
    'gsm' #OK
)
optdepends=(
    'libpng: Image format decoding.'				'lib32-libpng: Image format decoding.' #OK
    'libjpeg-turbo: Image format decoding.'			'lib32-libjpeg-turbo: Image format decoding.' #OK
    'libtiff: Image format decoding.'				'lib32-libtiff: Image format decoding.' #OK
    'libldap: LDAP remote directory protocol.'			'lib32-libldap: LDAP remote directory protocol.' #OK
    'libusb: USB device support.'				'lib32-libusb: USB device support.' #OK
    'gnutls: Cryptography'					'lib32-gnutls: Cryptography.' #OK
    'mpg123: mp3 decoding.'					'lib32-mpg123: mp3 decoding.' #OK
    'openal: Audio engine.'					'lib32-openal: Audio engine.' #OK
    'v4l-utils: Video capture.'					'lib32-v4l-utils: Video capture.' #OK
    'libpulse: Sound backend.'					'lib32-libpulse: Sound backend.' #OK
    'alsa-lib: Sound backend.'					'lib32-alsa-lib: Sound backend.' #OK
    'ocl-icd: Parallel computing / GPGPU.'			'lib32-ocl-icd: Parallel computing / GPGPU.' #OK
    'libxslt: XML parsing.'					'lib32-libxslt: XML parsing.' #OK
    'gstreamer: Multimedia playback.'				'lib32-gstreamer: Multimedia playback.' #OK
    'gst-plugins-base-libs: Multimedia playback.'		'lib32-gst-plugins-base-libs: Multimedia playback.' #OK
    'vulkan-icd-loader: Hardware-accelerated/3D graphics.'	'lib32-vulkan-icd-loader: Hardware-accelerated/3D graphics.' #OK
    'libcups: Printing.'					'lib32-libcups: Printing.' #OK
    'smbclient: Networking.' #OK

    'libxcomposite: Window management.'				'lib32-libxcomposite: Window management.'
    'libxinerama: Window management.'				'lib32-libxinerama: Window management.'

#    'giflib'                                                   'lib32-giflib'
#    'alsa-plugins'                                             'lib32-alsa-plugins'
#    'ncurses'               					'lib32-ncurses'
#    'dosbox'
)
options=('staticlibs')
install="$pkgname.install"
source=("https://github.com/ValveSoftware/wine/archive/refs/tags/${pkgname}-${pkgver}-${pkgrel}.tar.gz"
        '30-win32-aliases.conf'
        'wine-binfmt.conf')

if [ "$CARCH" = 'i686' ] 
then
    # strip lib32 etc. on i686
    _depends=(${_depends[@]/*32-*/})
    makedepends=(${makedepends[@]/*32-*/} ${_depends[@]})
    optdepends=(${optdepends[@]/*32-*/})
    provides=("wine=${pkgver}" "wine-valve=${pkgver}")
    conflicts=('wine' 'wine-staging' 'wine-staging-git')
else
    makedepends=(${makedepends[@]} ${_depends[@]})
    provides=("wine=${pkgver}" "bin32-wine=${pkgver}" "wine-wow64=${pkgver}" "wine-valve=${pkgver}")
    conflicts=('wine' 'wine-staging' 'wine-staging-git' 'bin32-wine' 'wine-wow64')
    replaces=('bin32-wine')
fi


prepare() {
    cd "wine-${pkgname}-${pkgver}-${pkgrel}/"
    
    # fix path of opencl headers
    sed 's|OpenCL/opencl.h|CL/opencl.h|g' -i configure*
}

build() {
    # delete old build dirs (from previous builds) and make new ones
    rm    -rf "$pkgname"-{32,64}-build
    mkdir -p  "$pkgname"-32-build
    
    # workaround for FS#55128
    # https://bugs.archlinux.org/task/55128
    # https://bugs.winehq.org/show_bug.cgi?id=43530
    export CFLAGS="${CFLAGS/-fno-plt/}"
    export LDFLAGS="${LDFLAGS/,-z,now/}"
    
    # build wine 64-bit
    # (according to the wine wiki, this 64-bit/32-bit building order is mandatory)
    if [ "$CARCH" = 'x86_64' ] 
    then
        msg2 'Building Wine-64...'

        mkdir "$pkgname"-64-build
        cd    "$pkgname"-64-build
        
        ../"wine-${pkgname}-${pkgver}-${pkgrel}"/configure \
                          --prefix='/usr' \
                          --libdir='/usr/lib' \
                          --with-x \
                          --with-gstreamer \
                          --enable-win64 \
			  --disable-tests
        make
        
        local _wine32opts=(
                    '--libdir=/usr/lib32'
                    "--with-wine64=${srcdir}/${pkgname}-64-build"
        )
        
        export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
    fi
    
    # build wine 32-bit
    msg2 'Building Wine-32...'
    
    cd "${srcdir}/${pkgname}"-32-build
    
    ../"wine-${pkgname}-${pkgver}-${pkgrel}"/configure \
                      --prefix='/usr' \
                      --with-x \
                      --with-gstreamer \
		      --disable-tests \
                      ${_wine32opts[@]}
    make
}

package() {
    depends=(${_depends[@]})
    
    # package wine 32-bit
    # (according to the wine wiki, this reverse 32-bit/64-bit packaging order is important)
    msg2 'Packaging Wine-32...'
    
    cd "$pkgname"-32-build
    
    if [ "$CARCH" = 'i686' ] 
    then
        make prefix="$pkgdir/usr" install
    else
        make prefix="$pkgdir/usr" \
             libdir="$pkgdir/usr/lib32" \
             dlldir="$pkgdir/usr/lib32/wine" install
    
        # package wine 64-bit
        msg2 'Packaging Wine-64...'
        
        cd "${srcdir}/${pkgname}"-64-build
        
        make prefix="$pkgdir/usr" \
             libdir="$pkgdir/usr/lib" \
             dlldir="$pkgdir/usr/lib/wine" install
    fi
    
    # font aliasing settings for Win32 applications
    install -d "$pkgdir"/etc/fonts/conf.{avail,d}
    install -m644 "${srcdir}/30-win32-aliases.conf" "${pkgdir}/etc/fonts/conf.avail"
    ln -s ../conf.avail/30-win32-aliases.conf       "${pkgdir}/etc/fonts/conf.d/30-win32-aliases.conf"
    
    # wine binfmt
    install -D -m644 "${srcdir}/wine-binfmt.conf"   "${pkgdir}/usr/lib/binfmt.d/wine.conf"

    #wine list.h
    for file in ${srcdir}/wine-${pkgname}-${pkgver}-${pkgrel}/include/wine/*.h; do
        cp -n $file "${pkgdir}/usr/include/wine/"
    done
}

sha256sums=('4859579b01ce8dfe876b35222916b960685a29e1a6e8dd7d741fd0fbf9c06d36' # tar.gz
            '9901a5ee619f24662b241672a7358364617227937d5f6d3126f70528ee5111e7'
            '6dfdefec305024ca11f35ad7536565f5551f09119dda2028f194aee8f77077a4')
