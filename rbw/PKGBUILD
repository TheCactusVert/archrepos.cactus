# Maintainer: Kevin Gavrois <kevin@gavrois.fr>

pkgname=rbw
pkgver=1.3.0
pkgrel=1
makedepends=('rust')
depends=('pinentry' 'openssl')
provides=('rbw')
arch=('i686' 'x86_64')
url='https://git.tozt.net/rbw'
source=("https://github.com/doy/${pkgname}/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('7255e322b8c6bd6ee9da00f4c54211e94a132abec15b9f51b8351af31a4744ac')
pkgdesc='Unofficial bitwarden CLI'
license=('MIT')

build() {
	cd $srcdir/$pkgname-$pkgver
	cargo build --release --locked
	cargo run --release --locked --bin rbw -- gen-completions bash >bash-completions
	cargo run --release --locked --bin rbw -- gen-completions zsh >zsh-completions
}

check() {
	cd $srcdir/$pkgname-$pkgver
	cargo test --release --locked
}

package() {
	cd $srcdir/$pkgname-$pkgver
	install -Dm755 target/release/rbw -t $pkgdir/usr/bin/
	install -Dm755 target/release/rbw-agent -t $pkgdir/usr/bin/
	install -Dm644 LICENSE -t $pkgdir/usr/share/licenses/$pkgname/
	install -Dm644 bash-completions $pkgdir/usr/share/bash-completion/completions/rbw
	install -Dm644 zsh-completions $pkgdir/usr/share/zsh/site-functions/_rbw
}
