# Maintainer: Kevin Gavrois <kevin@gavrois.fr>

_pkgname=rbw
pkgname=$_pkgname-cactus
pkgver=1.4.3.r2.g1e56f73
pkgrel=1
makedepends=('rust')
depends=('pinentry')
provides=('rbw')
conflicts=('rbw')
arch=('x86_64' 'aarch64')
url='https://git.tozt.net/rbw'
source=("$_pkgname::git+https://github.com/doy/rbw.git")
sha256sums=('SKIP')
pkgdesc='Unofficial bitwarden CLI'
license=('MIT')

pkgver() {
       cd $_pkgname
       git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd $_pkgname
	cargo build --release --locked
	cargo run --release --locked --bin rbw -- gen-completions bash >bash-completions
	cargo run --release --locked --bin rbw -- gen-completions zsh >zsh-completions
}

check() {
	cd $_pkgname
	cargo test --release --locked
}

package() {
	cd $_pkgname
	install -Dm755 target/release/rbw -t $pkgdir/usr/bin/
	install -Dm755 target/release/rbw-agent -t $pkgdir/usr/bin/
	install -Dm644 LICENSE -t $pkgdir/usr/share/licenses/rbw/
	install -Dm644 bash-completions $pkgdir/usr/share/bash-completion/completions/rbw
	install -Dm644 zsh-completions $pkgdir/usr/share/zsh/site-functions/_rbw
}
