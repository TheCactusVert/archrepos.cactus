# Maintainer: Kevin Gavrois <kevin@gavrois.fr>

_pkgname=xow
pkgname=xow
pkgver=0.5.r28.ga396e3f
pkgrel=1
pkgdesc='Linux driver for the Xbox One wireless dongle'
arch=('x86_64')
url='https://github.com/medusalix/xow'
license=('GPL2')
depends=('libusb')
makedepends=('cabextract')
conflicts=('xow-git')
source=("$_pkgname::git+https://github.com/medusalix/xow.git")
sha256sums=('SKIP')

pkgver() {
       cd $_pkgname
       git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd $_pkgname
	make PREFIX=/usr BUILD=RELEASE
}

package() {
	cd $_pkgname
	make PREFIX=/usr DESTDIR=$pkgdir SYSDDIR=/usr/lib/systemd/system install
}
